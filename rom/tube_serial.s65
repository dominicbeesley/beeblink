;-------------------------------------------------------------------------
;
; BeebLink - BBC Micro file storage system
; Copyright (C) 2018 Tom Seddon
; 
; This program is free software: you can redistribute it and/or
; modify it under the terms of the GNU General Public License as
; published by the Free Software Foundation, either version 3 of the
; License, or (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful, but
; WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program. If not, see
; <https://www.gnu.org/licenses/>.
;
;-------------------------------------------------------------------------
;
; The Master host code just gets straight to poking the Tube I/O as
; part of its EVENTV handler:
; 
; Send event across Tube
; ----------------------
; 0682 48          H    PHA
; 0683 A9 00       ).   LDA #&00
; 0685 20 91 06     ..  JSR &0691
; 0688 98          .    TYA
; 0689 20 91 06     ..  JSR &0691
; 068C 8A          .    TXA
; 068D 20 91 06     ..  JSR &0691
; 0690 68          h    PLA
; 
; Send byte in A via R1
; ---------------------
; 0691 2C E0 FE    ,`~  BIT &FEE0
; 0694 50 FB       P{   BVC &0691
; 0696 8D E1 FE    .a~  STA &FEE1
; 0699 60          `    RTS
; 
; So the Tube select bit of ACCCON needs some careful handling. If the
; ITU bit had to be disabled to gain access to the FTDI board, the
; access has to be performed with interrupts disabled.
;
;

                .section code

;-------------------------------------------------------------------------
                
                .virtual $fefe
fifo:
                .endv

                .virtual $feff
status: .block
rx_data=$01
tx_space=$02
suspend=$04
configured=$08
                .bend
                .endv

;-------------------------------------------------------------------------

tube_serial: .block

;-------------------------------------------------------------------------

; The minimum is actually 259 (see the docs/notes.org), but more won't
; hurt.
num_sync_zeros_required=500

                .section link_startup_workspace
num_sync_zeros_received: .fill 2
num_sync_zeros_sent: .fill 2
                .send link_startup_workspace

; The link startup workspace is a not-very-generous 8 bytes.
startup_end_time=brkthunk+0
startup_current_time=brkthunk+5
startup_max_time=250 ;in centiseconds
                
;-------------------------------------------------------------------------

                .section fs_private_workspace
                ; bit 7 set if M128, using internal Tube.
private_itu: .fill 1
                .send fs_private_workspace

;-------------------------------------------------------------------------
; Set a 16-bit value in zero page to 0.
;
; entry: X = zero page address of 16-bit value
; preserves X; Y

clrz16: .proc
                lda #0
                sta 0,x
                sta 1,x
                rts
                .pend

;-------------------------------------------------------------------------
; Compare a 16-bit value in zero page to num_sync_zeros_required.
;
; entry: X = zero page address of 16-bit value
; exit: Z=1 if value is num_sync_zeros_required
; preserves: X; Y
cmp_num_sync_zeros_required_z16: .proc
                lda 0,x
                cmp #<num_sync_zeros_required
                bne +

                lda 1,x
                cmp #>num_sync_zeros_required
+
                rts
                .pend
                
;-------------------------------------------------------------------------
; Check if ACCCON ITU needs resetting to access the FTDI device.
;
; In principle there's a race condition here, and ITU could become
; set. But it looks like the MOS actually just leaves it alone, so
; it's all quite safe(tm)...
;
; exit: C=0 if no handling required (B/B+/M128 with ext Tube or no Tube)
;       C=1 if handling required (M128 with int Tube)
; preserves: A; Y

is_itu: .proc
                pha
                
                ; http://beebwiki.mdfs.net/OSBYTE_&00
                lda #0
                ldx #1
                jsr osbyte

                ; X=0 (Electron), 1 (B), 2 (B+), 3 (Master), 4 (Master
                ; 128), 5 (Master Compact) - all other options appears
                ; to be non-6502.
                cpx #3
                bcc +           ;taken with C=0if not Master

                lda acccon
                and #acccon.itu
                cmp #acccon.itu ;C=1 if int Tube
+

                pla
                rts
                .pend

;-------------------------------------------------------------------------
; Reads status register. Handles Tube palaver if required.
;
; exit: A=value of status register
; preserves: Y
safe_read_status: .proc
                jsr is_itu
                bcs +
                lda status
                rts

+
                .cpu "65c02"
                phy
                ldy #status-fifo
                jsr m128_read_register
                ply
                .cpu 'default'
                rts
                .pend

m128_read_register: .proc
                .cpu "65c02"
                php
                sei
                
                ldx acccon
                lda #acccon.itu
                trb acccon
                
                lda fifo,y

                stx acccon
                plp
                .cpu "default"
                rts
                .pend

;-------------------------------------------------------------------------
; Reads FIFO. Handles Tube palaver if required.
;
; exit: C=0 if no data was available
;       C=1 if data was available, A = value read
; preserves: Y
safe_read_fifo: .proc
                jsr safe_read_status
                and #status.rx_data
                bne got_data

no_data:
                clc             ;no data available
                rts

got_data:
                jsr is_itu
                bcs +
                
                lda fifo
                sec
                rts

+
                .cpu "65c02"
                phy
                ldy #fifo-fifo
                jsr m128_read_register
                sec
                rts
                .cpu 'default'
                .pend

;-------------------------------------------------------------------------
; Writes FIFO. Handles Tube palaver if required.
;
; entry: A=value to write
; exit: C=0 if no space
;       C=1 if data was written
; preserves Y
safe_write_fifo: .proc
                pha
                jsr safe_read_status
                and #status.tx_space
                bne got_space

no_space:
                pla
                clc             ;no space available
                rts

got_space:
                jsr is_itu
                bcs m128_write_fifo

                pla
                sta fifo
                sec
                rts

m128_write_fifo:
                .cpu "65c02"
                php
                sei

                ldx acccon
                lda #acccon.itu
                trb acccon

                pla
                sta fifo

                stx acccon
                plp
                .cpu 'default'
                rts
                .pend
                
;-------------------------------------------------------------------------

startup: .proc

                ; jsr pcprint
                ; .text "borkork",255

                ; sec
                ; rts

                ldx #25         ;# vsyncs to wait
loop:
                txa
                pha
                
                lda #19
                jsr osbyte

                jsr safe_read_status
                
                ; bit 7 of $feff seems to be reliably set if the Tube
                ; serial board isn't connected.
                cmp #$80
                bcc maybe_present

                pla             ;discard
                jmp not_present

maybe_present:
                ; wait for bit 3 to become set.
                and #status.configured
                cmp #status.configured ;C=1 if bit 3 set

                pla
                tax

                bcs good

                dex
                bne loop
                
not_ready:
                ldx #status_text.device_not_ready-status_text
                sec             ;no good
                rts

not_present:
                ldx #status_text.device_not_present-status_text
                sec             ;no good
                rts

good:

                lda #1          ;read system clock
                ldx #<startup_end_time
                ldy #>startup_end_time
                jsr osword

                .print ["Time now:  &",startup_end_time+4,startup_end_time+3,startup_end_time+2,startup_end_time+1,startup_end_time+0,"\r"]

                clc
                lda startup_end_time
                adc #startup_max_time
                sta startup_end_time
                bcc +
                inc startup_end_time+1
                bne +
                inc startup_end_time+2
                bne +
                inc startup_end_time+3
                bne +
                inc startup_end_time+4
+
                
                .print ["End time: &",startup_end_time+4,startup_end_time+3,startup_end_time+2,startup_end_time+1,startup_end_time+0,"\r"]

start_sync:
                ldx #num_sync_zeros_received
                jsr clrz16

                ldx #num_sync_zeros_sent
                jsr clrz16

sync_loop:
                jsr check_startup_timeout
                bcs timed_out
                
                jsr send_sync_zero
                
                ; Try to get more data. If none available, just carry
                ; on looping.
                jsr safe_read_fifo
                bcc sync_loop

                ; If a non-0 was received, restart the sync process.
                cmp #0
                bne start_sync

                ; Count one more zero received.
                ldx #num_sync_zeros_received
                jsr add1z16

                ; Keep going if required number not reached.
                jsr cmp_num_sync_zeros_required_z16
                bne sync_loop
                
                ; Looks like server is definitely in sync mode. Ensure
                ; that num_sync_zeros_required 0s get sent, so it
                ; knows the ROM is in sync mode too.
send_any_remaining_zeros_loop:
                ldx #num_sync_zeros_sent
                jsr cmp_num_sync_zeros_required_z16
                beq send_non_zero

                jsr send_sync_zero
                jmp send_any_remaining_zeros_loop

                ; Write a single non-zero byte.
send_non_zero:
                jsr check_startup_timeout
                bcs timed_out
                
                lda #1
                jsr safe_write_fifo
                bcc send_non_zero

                ; Keep reading until a non-zero byte is encountered.
                ; If it's 1, that's a successful sync; otherwise,
                ; something's gone wrong.
receive_zeros_loop:
                jsr check_startup_timeout
                bcs timed_out
                
                jsr safe_read_fifo
                bcc receive_zeros_loop

                cmp #0
                beq receive_zeros_loop

                cmp #1
                beq ok

                ldx #status_text.sync_failed-status_text
                sec             ;no good
                rts

timed_out:
                ldx #status_text.sync_timed_out-status_text
                sec
                rts

ok:
                clc             ;good
                rts

reset_sync_counter:
                rts

send_sync_zero:
                lda #0
                jsr safe_write_fifo
                bcc +           ;taken if write failed

                ldx #num_sync_zeros_sent
                jsr add1z16
+
                rts

check_startup_timeout:
                lda #1          ;read system clock
                ldx #<startup_current_time
                ldy #>startup_current_time
                jsr osword

                sec
                .for i=0,i<=4,i+=1
                lda startup_end_time+i
                sbc startup_current_time+i
                .next

                bpl no_timeout

timeout:
                .print ["Timeout time: &",startup_current_time+4,startup_current_time+3,startup_current_time+2,startup_current_time+1,startup_current_time+0,"\r"]
                sec
                rts

no_timeout:
                clc
                rts
                
                .pend

status_text: .block
device_not_ready:
                .text 'Device not ready',0
device_not_present:
                .text 'Device not present',0
sync_failed:
                .text 'Sync failed',0
sync_timed_out:
                .text 'Sync timeout',0
                .bend
                
;-------------------------------------------------------------------------
; Prepare link.
;
; Set Master 128 ITU flag... and anything else???
prepare: .proc
                jsr is_itu
                ror private_itu

                bcc +
                .brk_error 255,"Internal Tube = TODO",255
+
                rts
                .pend

;-------------------------------------------------------------------------
; Unprepare link.
;
; (Nothing needs to be done for the Tube serial.)
unprepare: .proc
                rts
                .pend

;-------------------------------------------------------------------------
; Placeholder for stuff I haven't done yet.              
error: .proc
                .brk_error 255,"Not supported"
                .pend

;-------------------------------------------------------------------------
; Placeholder for stuff that doesn't need doing.
nop: .proc
                rts
                .pend

                
;-------------------------------------------------------------------------

                .bend
                
                .send code

;-------------------------------------------------------------------------
                
link_name="Tube Serial"
link_prepare=tube_serial.prepare
link_unprepare=tube_serial.unprepare

link_begin_recv=tube_serial.nop
link_begin_send=tube_serial.nop

link_send_header_byte=tube_serial.error
link_send_payload_byte=tube_serial.error
link_recv_header_byte=tube_serial.error
link_recv_payload_byte=tube_serial.error
                
link_startup=tube_serial.startup
link_status_text=tube_serial.status_text
                
link_send_file_data_parasite=send_file_data_parasite_bytewise
link_send_file_data_host=send_file_data_host_bytewise
link_recv_file_data_parasite=recv_file_data_parasite_bytewise
link_recv_file_data_host=recv_file_data_host_bytewise
